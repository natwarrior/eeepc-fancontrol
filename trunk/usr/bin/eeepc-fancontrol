#!/usr/bin/perl -l
################################################
# eeePC Fan Control Daemon 0.21
#  Author       : raimoradczewski at googlemail dot com
#  Author Blog  : http://www.ireemo.de/ (english)
#  Project Home : http://code.google.com/p/eeepc-fancontrol/
#
# Needs eee.ko Module
# Version 0.21

use strict;
use Sys::Syslog qw (:standard :macros);

## Options
# Check Temperature every X seconds
my $interval	=	1;

# Maximum/Minimum Fan Speed
my $maxSpeed	=	100;
my $minSpeed	=	0;

# Temperatures for when to set $maxSpeed / $minSpeed
my $highTemp	=	60;
my $normalTemp 	=	50;


## DO NOT CHANGE ANYTHING BELOW THIS ##
my $jump	=	($maxSpeed-$minSpeed)/($highTemp-$normalTemp);
my $startSpeed	=	40;
my $stopSpeed	=	14;
our $temperature;

$SIG{KILL} = 'terminateFancontrol';
$SIG{TERM} = 'terminateFancontrol';
$SIG{INT}  = 'terminateFancontrol';

openlog("eeepc-fancontrol", "ndelay", "local0");

# Print some Information at syslog
syslog(LOG_INFO, "FanControl started with:");
syslog(LOG_INFO, " MinSpeed: ".$minSpeed."% MaxSpeed: ".$maxSpeed."%");
syslog(LOG_INFO, " NormalTemp: ".$normalTemp."°C MaxSpeed: ".$highTemp."°C");
syslog(LOG_INFO, " Calculated Speed/C: ".$jump);

# Checking for Module 'eee' to be present
checkModule();

# Entering Main Loop
while (1) {
	checkFanOverride();
	checkFanRPM();
	$temperature = getTemperature();
	if ($temperature < $normalTemp) { setFanSpeed($minSpeed); }
	elsif($temperature > $highTemp) { setFanSpeed($maxSpeed); }
	else { setFanSpeed($minSpeed+$jump*($temperature-$normalTemp));
	}
	sleep($interval);
}
closelog();

sub checkFanRPM() {
	if(getFanRPM() eq 0 && getFanSpeed() ne 0) {
		syslog(LOG_INFO, "Fan stopped spinning. Speed raised to: ".$startSpeed."%");
		setFanSpeed($startSpeed);
		sleep(1);
	}
}

sub setFanSpeed {
	my $speed = int(shift);
	$speed = $stopSpeed if($speed > 0 and $speed < $stopSpeed);
	return if($speed eq getFanSpeed());
	syslog(LOG_INFO, "Speed set to: ".$speed."% (Temp: ".$temperature."°C)");

	open(TEMP, ">/proc/eee/fan_speed");
	print TEMP $speed;
	close(TEMP);
}

sub terminateFancontrol {
	FanOverride(0);
	syslog(LOG_INFO, "Hardware Fan Control restored");
	syslog(LOG_INFO, "Shutting down...");
	exit(0);
}

sub getFanSpeed {
	open(TEMP, "</proc/eee/fan_speed");
	my $currentSpeed = <TEMP>; chomp($currentSpeed);
	close(TEMP);
	return $currentSpeed;
}

sub getFanRPM {
	open(TEMP, "</proc/eee/fan_rpm");
	my $currentRPM = <TEMP>; chomp($currentRPM);
	close(TEMP);
	return $currentRPM;
}

sub getTemperature {
	open(TEMP, "</proc/eee/temperature");
	my $currentTemperature = <TEMP>; chomp($currentTemperature);
	close(TEMP);
	return $currentTemperature;
}

sub checkFanOverride {
	open(TEMP, "</proc/eee/fan_manual");
	my $currentFanState = <TEMP>; chomp($currentFanState);
	FanOverride(1) if($currentFanState eq 0);
}

sub FanOverride {
	my $state = shift;
	open(TEMP, ">/proc/eee/fan_manual");
	print TEMP $state;
	close(TEMP);
	syslog(LOG_INFO, "Fan Control Override triggered");
}

sub checkModule {
	if (`lsmod` !~ /eee/) {
		system("modprobe eee");
		syslog(LOG_INFO, "Inserted EEE Module");
	}
}
